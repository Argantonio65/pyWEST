/*******************************************************************************
 *
 * Tornado - Advanced Kernel for Modeling and Virtual Experimentation
 * (c) Copyright 2004-2011 DHI
 *
 * This file is provided under the terms of a license and may not be
 * distributed and/or modified except where allowed by that license.
 *
 * This file is provided as is with no warranty of any kind, including the
 * warranty of design, merchantability and fitness for a particular purpose.
 *
 * $Revision: 1$
 * $Date: 23. marts 2012 12:41:31$
 *
 ******************************************************************************/

#ifndef WWTP_BASE_INTERNAL_MEMBRANE_MEMBRANEIDEAL
#define WWTP_BASE_INTERNAL_MEMBRANE_MEMBRANEIDEAL

CLASS MembraneIdeal
 (* icon = "membrane" *)
 "Simple membrane filtration model"
 EXTENDS IdealMembraneVolumeClass WITH
 {:

  comments <- "A model for an external membrane unit (fouling is not modelled)";

  interface <-
  {
  OBJ X_Out (* terminal = "out_2" *) "TSS in the outflow" : Concentration := {: causality <- "COUT" ; group <- "Permeate" :};
  OBJ X_Concentrate (* terminal = "out_2" *) "TSS in the concentrate" : Concentration := {: causality <- "COUT" ; group <- "Concentrate" :};
  OBJ PumpingEnergy (* terminal = "out_2" *)"Pumping energy" : ElectricalEnergy := {: causality <- "COUT"; group <- "Energy" :};
  };

  parameters <-
  {
  OBJ V_Tank "Tank volume " : Volume := {: value <- 100; group <- "Dimension" :};
  #ifdef CalculateTSSComponent
  OBJ F_TSS_COD "Fraction TSS/COD" : Fraction := {: value <- 0.75 ; group <- "Conversion factors" :};
  #endif // CalculateTSSComponent
// PUMPING ENERGY
    OBJ TMP "Trans-membrane pressure" : PressureDifference := {: value <- 35000 ; group <- "Energy" :};
    OBJ n_Pump_Membrane "Pump efficiency of the membrane pump" : Fraction := {: value <- 0.75 ; group <- "Energy":};
  };

  state <-
  {
// PUMPING ENERGY
    OBJ Integ_PE_Membrane (* hidden = "1" *)"Integral Pumping energy for the transmembrane pump" : ElectricalEnergy ;
  };
    
  initial <-
  {
  state.M[IndexOfSolvent] = parameters.V_Tank / 0.000001; 
  };

  equations <-
  {
  state.Q_Out_Membrane = IF ((state.Q_In - state.Q_Out_Membrane_Help) <= 0)
      THEN state.Q_In
    ELSE state.Q_Out_Membrane_Help;

  state.Q_Concentrate = IF ((state.Q_In - state.Q_Out_Membrane) <= 0)
      THEN 0
    ELSE state.Q_In - state.Q_Out_Membrane;

  #ifdef CalculateTSSComponent
  interface.X_Out = (SUMOVER Comp_Index IN {IndexOfFirstTSSComponent .. IndexOfLastTSSComponent}:
      (- interface.Outflow[Comp_Index]) * parameters.F_TSS_COD);
  interface.X_Concentrate = (SUMOVER Comp_Index IN {IndexOfFirstTSSComponent .. IndexOfLastTSSComponent}:
      (- interface.OutflowConcentrate[Comp_Index]) * parameters.F_TSS_COD);
  #endif // CalculateTSSComponent
  // ASM2-type category
  #ifdef UseTSSComponent
  interface.X_Out = (- interface.Outflow[IndexOfTSSComponent]) ;
  interface.X_Concentrate = (- interface.OutflowConcentrate[IndexOfTSSComponent]) ;
  #endif // UseTSSComponent 

// PUMPING ENERGY
  DERIV(state.Integ_PE_Membrane,[independent.t])=  state.Q_Out_Membrane * parameters.TMP / (3600000.0 * parameters.n_Pump_Membrane);
// 3600 conversion h to sec
// 1000 conversion from Pa to kPa 
  interface.PumpingEnergy = state.Integ_PE_Membrane;
  };

 :};

#endif    // WWTP_BASE_INTERNAL_MEMBRANE_MEMBRANEIDEAL
