// ------------------------------------------------------------------------------------------------------------
//  TUDELFT - QUICS
//  Implementation: Antonio Moreno Rodenas, Vasilis Bellos
//  Description: MSL-USER/River/VolumeConsersionModel body
//  IMPLEMENTATION IN TEST. NUMERICAL INSTABILITIES. DO NOT USE IT
// ------------------------------------------------------------------------------------------------------------

CLASS RivVarVolumeRIVER_MCT
 (* icon = "River" *)
 SPECIALISES RivVarVolumeConversionModel :=
 {:
  comments <- "Model for a bulk water unit with a variable volume and biochemistry from muskingum-Cunge Todini approximation for the diffusive term of wave propagation. DO NOT USE IT, the implementation presents numerical instabilities";
  interface <-{};
  parameters <-{
  //////////////////////////HYDRAULICS PARAMETERS///////////////////
	OBJ W_b "Width of channel at bottom of bank" : Length := {: value <- 10; group <- "River characteristics" :};
	OBJ L "Length of channel" : Length := {:value <- 2000; group <- "River characteristics" :};
	OBJ z "Banks slope" : Real := {:value <- 0.5; group <- "River characteristics" :};	
	OBJ slp "Average channel slope along channel length" : Ratio := {:value <- 0.0005; group <- "River characteristics" :};
	OBJ n "Manning's 'n' value for channel" : Real := {:value <- 0.035; group <- "River characteristics" :};
	
  OBJ d_min "Minimum depth of water" : Length := {:value <- 0.05; group <- "River characteristics" :};
	OBJ Vol_min "Minimum volume of water" : Volume := {: group <- "River characteristics" :};
	OBJ Vol_init "Initial volume of water as ratio on minimum" : Real := {:value <- 1.5; group <- "River characteristics" :};  
	
	/////// NewtonRaphson parameters
  OBJ Niter "N iterations of the newton raphson routine, calculation of depth from flow for trapezoidal section" : Real := {:value <- 10; group <- "Newton Raphson Parameters" :};	
  
  ////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

  //////////////////////////BIOCHEMISTRY PARAMETERS///////////////////////////////////////////////////////////////////////////////////////
  OBJ alpha "Alpha factor for artificial aeration" : Real := {:value <- 0.85:};	
  OBJ pH "pH" : Real := {:value <- 7.5:};	
  OBJ KLmin "Minimum mass transfer constant reaeration": Velocity := {:value <- 0.1; unit <- "m/d":};
	OBJ TKL "Temperature coefficient mass transfer": Real := {:value <- 1.02:};
	OBJ Kd1 "Decay rate BOD1": RateConstant := {:value <- 0.4; unit <- "1/d":};
	OBJ Kd2 "Decay rate BOD2": RateConstant := {:value <- 0.3; unit <- "1/d":};
	OBJ KBODs "Decay rate temporary sediment BOD": RateConstant := {:value <- 0.7; unit <- "1/d":};
	OBJ Vs1 "Sedimentation rate particulate BOD1": Velocity := {:value <- 10; unit <- "m/d":};
	OBJ Vs2 "Sedimentation rate particulate BOD2": Velocity := {:value <- 50; unit <- "m/d":};
	OBJ KO2 "Monod constant O2 on BOD decay": Concentration := {:value <- 2.0; unit <- "mg/l":};
	OBJ TKd "Temperature coefficient DOD decay": Real := {:value <- 1.05:};
	OBJ TKp "Temperature coefficient plant growth/decay": Real := {:value <- 1.03:};
	OBJ Knit "Nitrification rate": RateConstant := {:value <- 0.2; unit <- "1/d":};
	OBJ TKnit "Temperature coefficient nitrification": Real := {:value <- 1.06:};
	OBJ KNO2 "Monod constant O2 on nitrification": Concentration := {:value <- 3.0; unit <- "mg/l":};
	OBJ KSO2 "Monod constant O2 on sediment oxygen demand": Concentration := {:value <- 2.0; unit <- "mg/l":};
	OBJ ProdM "Oxygen production Macrophyte Biomass (gO2/gM)/(W/m2)/day": Real := {:value <- 0.0002:};
	OBJ ResM "Oxygen respiration Macrophyte Biomass (gO2/gM)/day": RateConstant := {:value <- 0.04; unit <- "1/d":};
	OBJ TSOD "Temperature coefficient SOD": Real := {:value <- 1.08:};
	OBJ SBOD1 "Diffuse source BOD1, sediment (g/m2/day)" : Real := {:value <- 0.0; group <- "Inputs":};
	OBJ SBOD2 "Diffuse source BOD2, sediment (g/m2/day)" : Real := {:value <- 0.0; group <- "Inputs":};
	OBJ SNH4 "Diffuse source NH4, sediment (g/m2/day)" : Real := {:value <- 0.0; group <- "Inputs":};
	OBJ SOD "Sediment Oxygen Demand (g/m2/day)" : Real := {:value <- 5.0; group <- "Inputs":};	
	OBJ VKL "Velocity reaeration coefficient": Real := {:value <- 3.2:};

	OBJ MB "Macrophyte biomass" : SurfaceDensity := {:value <- 50; group <- "Inputs":};

	OBJ T_ref "Air/water T conversion: T_wat=T_air+(T_ref-T_air)/T_par" : CelsiusTemperature := {:value <- 15:};
	OBJ T_par "Air/water T conversion: T_wat=T_air+(T_ref-T_air)/T_par" : Real := {:value <- 3:};
	////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	};
  state <-{
  ///////////////////////////STATES HYDRAULICS////////////////////////////////////////////////////////////////////////////////////////////
  OBJ C0 "Muskingum coefficient C0" : Real:= {: group <- "Muskingum routing" :};
  OBJ C1 "Muskingum coefficient C1" : Real:= {: group <- "Muskingum routing" :};
  OBJ C2 "Muskingum coefficient C2" : Real:= {: group <- "Muskingum routing" :};
 
  OBJ Dt "Timestep" : Real:= {: group <- "Muskingum routing" :};
  OBJ Q_help "Flow" : FlowRate := {: group <- "River characteristics" :};
  
  OBJ Q_Out_i "Flow" : FlowRate := {: group <- "River characteristics" :};
  OBJ Q_ref "Flow" : FlowRate := {: group <- "River characteristics" :};
	OBJ d_ref "Depth of water in channel" : Length:= {: group <- "River characteristics" :};
  OBJ c_ref  "Celerity wave water tank initial" : Velocity:= {: group <- "River characteristics" :};
  OBJ b_ref  "Correcting factor for steady state compliance" : Real:= {: group <- "River characteristics" :};
  OBJ Cour_ref "Corrected Courant number" : Real:= {: group <- "River characteristics" :};
  OBJ Cour_help "Corrected Courant number" : Real:= {: group <- "River characteristics" :};  
  OBJ Dre_ref "Corrected cell Reynolds number" : Real:= {: group <- "River characteristics" :};
  OBJ A_ref "Cross-sectional area bulk water tank" : Area:= {: group <- "River characteristics" :};
  OBJ P_ref "Cross-sectional area bulk water tank" : Length:= {: group <- "River characteristics" :};
  OBJ B_ref "Cross-sectional area bulk water tank" : Length:= {: group <- "River characteristics" :};
  
	OBJ A "Cross-sectional area bulk water tank" : Area:= {: group <- "River characteristics" :};
	OBJ d "Depth of water in channel" : Length:= {: group <- "River characteristics" :};
	OBJ d_help (* hidden="1" *) "Depth of water in channel" : Length:= {: group <- "River characteristics" :};
	OBJ vel_water "Velocity of water" : Velocity := {: group <- "River characteristics" :};
	OBJ V_help (* hidden="0" *) "Help volume" : Volume:= {: group <- "River characteristics" :};
  ////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
  	OBJ d_test "Depth of water in channel" : Length:= {: group <- "River characteristics" :};
  //////////////////////////////STATES BIOCHEMISTRY///////////////////////////////////////////////////////////////////////////////////////
  OBJ CS "saturation conc Oxygen" : Real ;
  OBJ DO_sat_perc "saturation percentage Oxygen" : Fraction ;
  OBJ UIamm "unionised ammonia" : Concentration ;
  OBJ UIamm_corr "unionised ammonia, corrected for DO" : Concentration ;
  OBJ DO_corr "DO, corrected fro UIamm" : Concentration ;
	OBJ KL20 "mass transfer at 20oC calculated form velocity and depth" : Real ;
	OBJ KLT "" : Real ;

	OBJ PO2M "net oxygen production/consumption Macrophytesn" : Real ;
	OBJ SedO2 "" : Real ;
	OBJ REAR "" : Real ;
	OBJ Nitrif "" : Real ;
	OBJ BODox "" : Real ;
	OBJ SODT "" : Real ;
	OBJ BOD "" : Real ;
	OBJ T_wat "Water temperature" : CelsiusTemperature ;
	
	OBJ KL20_help (* hidden = "1" *) "" : Real ;
	OBJ T_wat_help (* hidden = "1" *) "Water temperature" : CelsiusTemperature ;

	OBJ OTR "Actual oxygen transfer rate" : MassFlux;
  OBJ SOTR "Standard oxygen transfer rate" : MassFlux;
  /////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
   	};
	  
   initial <-{   
   parameters.Vol_min = parameters.d_min * (parameters.W_b + parameters.d_min * parameters.z) * parameters.L;
   ////// INITAL CONDITIONS FOR McCarthy-Muskingum with variable parameters
   //CALCULATE DEPTH from initial volume. It allows proper initial conditions to be specified from another dynamic experiment.
   /*
   state.M[rH2O] = IF (state.M[rH2O] > 0 ) THEN state.M[rH2O] ELSE parameters.L * (parameters.W_b + parameters.z) * 1000000; // If initial volume is 0, then assumes a volume associated to 1m depth.
   
   state.d_help = (sqrt(pow(parameters.W_b,2) + 4*parameters.z * state.M[rH2O]*0.000001/parameters.L) - parameters.W_b ) / (2*parameters.z);
   state.d = IF (state.d_help <= parameters.d_min) THEN 0.5 ELSE state.d_help;
     
   state.V = state.M[1] * 0.000001 ;
   
   state.Q_Out = (sqrt(parameters.slp)/parameters.n)* (pow( parameters.W_b * state.d + parameters.z * pow(state.d, 2)  ,(5.0/3.0))   ) / pow(parameters.W_b + 2* state.d * sqrt(1 + pow(parameters.z,2) ), (2.0/3.0)); 
   state.Q_In = state.Q_Out;
   state.Q_help = state.Q_Out;
   */
   ////
   //Initial with 10 m3/s
   state.Q_Out = 10 * (3600*24);
   state.Q_In = 10 * (3600*24);
   state.Q_help = state.Q_Out;
   
   state.d= NewtonRaphson(1, 10/(3600*24), parameters.W_b, parameters.slp, parameters.z, parameters.n, parameters.Niter);
   state.d_ref = state.d;
   state.M[rH2O] = 1000000 * state.d * ( parameters.W_b + state.d * parameters.z) * parameters.L; 
   state.V =  state.M[rH2O] * 0.000001;
   //////////////////
   
   state.A_ref = state.d * ( parameters.W_b + state.d * parameters.z); 
   state.P_ref = parameters.W_b + 2 * state.d * sqrt(1+ pow(parameters.z,2)); 
   state.B_ref = parameters.W_b + 2 * state.d * parameters.z;
  
   state.c_ref = ((5.0 / 3.0) * (sqrt(parameters.slp) / parameters.n) * pow(state.A_ref, 2.0/3.0) / pow(state.P_ref,2.0/3.0) ) * (1- (4.0/5.0)*state.A_ref * sqrt(1+pow(parameters.z,2)) / (state.B_ref * state.P_ref)  ); //m/s
   state.b_ref = state.c_ref * state.A_ref / (state.Q_Out/(3600*24)); //(5.0/3.0)*(1- (4.0/5.0)*state.A_ref * sqrt(1+pow(parameters.z,2)) / (state.B_ref * state.P_ref)  );
  
   state.Cour_ref = state.c_ref * state.Dt * (3600*24) / (state.b_ref * parameters.L) ;
   state.Dre_ref = (state.Q_Out/(3600*24)) / (state.b_ref * state.B_ref * parameters.slp * state.c_ref * parameters.L);
   
    state.C0 = 1/3;
    state.C1 = 1/3;
    state.C2 = 1/3;
    };
 
 
  equations <-{
  ///////////////////////////////////////DERIVATIVES/////////////////////////////////////////////////////
  // The FluxPerComponent is the sum of all
    // incoming (positive) and outgoing (negative) fluxes 

    {FOREACH Comp_Index IN {1 .. RivNrOfComponents}:
      state.FluxPerComponent[Comp_Index] =

    (SUMOVER In_RivTerminal IN {SelectByType(interface,InRivTerminal)}:
    In_RivTerminal[Comp_Index])+
    (SUMOVER Out_RivTerminal IN {SelectByType(interface,OutRivTerminal)}:
    Out_RivTerminal[Comp_Index]);};

    // The mass balance equations.
    // These are composed of a term due to incoming and
    // outgoing fluxes and of a term due to biochemical
    // interactions between components.

    {FOREACH Comp_Index IN {1 .. RivNrOfComponents}:
      DERIV(state.M[Comp_Index],[independent.t]) = 
      state.FluxPerComponent[Comp_Index]
      +state.ConversionTermPerComponent[Comp_Index];};

    // for efficiency and because most models need it anyway
    // we calculate Q_In here
    
    {FOREACH Comp_Index IN {1 .. RivNrOfComponents}:
      state.InFluxPerComponent[Comp_Index] =
         SUMOVER In_RivTerminal IN {SelectByType(interface,InRivTerminal)}:
            (In_RivTerminal[Comp_Index]);
    };

    {state.Q_In = (0.000001
                  * state.InFluxPerComponent[rH2O]);
    };
  
  //////////////////////////////////////EQUATIONS HYDRAULICS//////////////////////////////////////////////////////////////////////////////

  state.Dt = (independent.t - previous(independent.t)); // days
    
  //Initial flow estimation
  //state.Q_Out_i = previous(state.Q_Out) + (state.Q_In - previous(state.Q_In));   // m3/d
  
    //ad hoc initial conditions.
  state.Q_Out_i =  IF (independent.t <= state.Dt) 
	THEN 10*3600*24  //state.C0_i*state.Q_In + state.C1_i*state.Q_In + state.C2_i*state.Q_Out
	ELSE previous(state.Q_Out) + (state.Q_In - previous(state.Q_In));   // m3/d
  
  
  
  state.Q_ref = (state.Q_In + state.Q_Out_i) / 2 ; //m3/d
  
  state.d_ref = IF( state.Q_ref> 0) 
                THEN NewtonRaphson(1, state.Q_ref / (3600*24), parameters.W_b, parameters.slp, parameters.z, parameters.n, parameters.Niter) 
                ELSE 0.1;
  
//state.Q_Out_i/(3600*24)
  state.A_ref = state.d_ref * ( parameters.W_b + state.d_ref * parameters.z); 
  state.P_ref = parameters.W_b + 2 * state.d_ref * sqrt(1+ pow(parameters.z,2)); 
  state.B_ref = parameters.W_b + 2 * state.d_ref * parameters.z;
  
  state.c_ref = ((5.0 / 3.0) * (sqrt(parameters.slp) / parameters.n) * pow(state.A_ref, 2.0/3.0) / pow(state.P_ref,2.0/3.0) ) * (1- (4.0/5.0)*state.A_ref * sqrt(1+pow(parameters.z,2)) / (state.B_ref * state.P_ref)  ) * (3600*24); //m/s

  state.b_ref = state.c_ref * state.A_ref / state.Q_ref;
  state.Cour_help = (state.c_ref / state.b_ref)*( state.Dt / parameters.L);
  state.Cour_ref = IF(state.Cour_help<=0.001) 
                   THEN 0.001 
                   ELSE state.Cour_help;
                   
  state.Dre_ref = (state.Q_ref) / (state.b_ref * state.B_ref * parameters.slp * state.c_ref * parameters.L);
  
    
  state.C0 = (-1 + state.Cour_ref + state.Dre_ref) / (1 + state.Cour_ref + state.Dre_ref);
  state.C1 = ((1 + previous(state.Cour_ref) - previous(state.Dre_ref)) / (1 + state.Cour_ref + state.Dre_ref) ) * (state.Cour_ref / previous(state.Cour_ref));
  state.C2 = ((1 - previous(state.Cour_ref) + previous(state.Dre_ref)) / (1 + state.Cour_ref + state.Dre_ref) ) * (state.Cour_ref / previous(state.Cour_ref));
  
  
  state.Q_help = state.C0*state.Q_In + state.C1*previous(state.Q_In) + state.C2*previous(state.Q_Out);
  
  state.Q_Out = IF (state.Q_help <= 0) 
  THEN 0 
  ELSE state.Q_help;
  
  

  state.d = IF( state.Q_Out > 0) 
            THEN NewtonRaphson(1, state.Q_Out / (3600*24), parameters.W_b, parameters.slp, parameters.z, parameters.n, parameters.Niter) 
            ELSE 0.1;
  
  
  
	state.V_help = (SUMOVER Comp_Index IN {1 .. RivNrOfComponents}:
	(0.000001*state.M[Comp_Index]));		
	state.V =  IF (state.V_help <= parameters.Vol_min) 
	THEN parameters.Vol_min 
	ELSE state.d*(parameters.W_b + state.d * parameters.z) * parameters.L;

	state.A = state.V / parameters.L;
	state.vel_water = state.Q_Out / state.A; // m/d
	//state.d = IF (parameters.z > 0)
	//THEN  sqrt(state.A / parameters.z + pow((parameters.W_b / 2 / parameters.z),2)) - (parameters.W_b / 2 / parameters.z)
	//ELSE  state.A / parameters.W_b ; 
  
   {FOREACH Comp_Index IN {1 .. RivNrOfComponents}:state.C[Comp_Index] = IF (state.V == 0)
			   THEN 0
			   ELSE state.M[Comp_Index]/state.V;
			   };

   {FOREACH Comp_Index IN {1 .. rBOD2p}:
     interface.Outflow[Comp_Index] = - state.C[Comp_Index] * state.Q_Out ;
     };	
	//interface.Outflow[rH2O] = - state.Q_Out * 1000000;
	interface.Outflow[rBODs] = 0;
	

//////////////////////////EQUATIONS BIOCHEMISTRY //////////////////////////////////////////////////////////////////////////////////////////
  state.OTR = interface.kLa * (state.CS - state.C[rO2]) * state.V;
  state.SOTR = state.OTR * state.CS / (state.CS - state.C[rO2]) / parameters.alpha;
	
	state.T_wat_help = interface.T + (parameters.T_ref - interface.T) / parameters.T_par;
	state.T_wat = IF (state.T_wat_help > 0)
				        THEN state.T_wat_help
				        ELSE 0;

	state.CS = 14.652 - 0.41022 * state.T_wat + 0.007991 * pow(state.T_wat,2.0) - 0.000077774 * pow(state.T_wat,3.0);
	state.KL20_help = parameters.VKL * pow((state.vel_water/3600/24),0.67) * pow(state.d,-0.85);
	state.KL20 = IF (state.KL20_help < parameters.KLmin)
			         THEN parameters.KLmin
				       ELSE state.KL20_help;
	state.KLT = state.KL20 * pow(parameters.TKL,(state.T_wat-20));
	state.PO2M = (parameters.ProdM * interface.I0 - parameters.ResM) * parameters.MB / state.d * pow(parameters.TKp,(state.T_wat-20));
	state.SedO2 = (- parameters.SOD / state.d) * pow(parameters.TSOD,(state.T_wat-20)) * state.C[rO2] / (state.C[rO2] + parameters.KSO2);
	state.REAR = (state.KLT / state.d + interface.kLa) * (state.CS - state.C[rO2]);
	state.Nitrif = -4.57 * parameters.Knit * state.C[rNH4] * pow(parameters.TKnit,(state.T_wat-20)) * state.C[rO2] / (state.C[rO2] + parameters.KNO2);
	state.BODox = (- parameters.Kd1 * (state.C[rBOD1] + state.C[rBOD1p]) - parameters.Kd2 * (state.C[rBOD2] + state.C[rBOD2p])  ) * pow(parameters.TKd,(state.T_wat-20)) * state.C[rO2] / (state.C[rO2] + parameters.KO2);
	state.SODT = - state.C[rBODs] * parameters.KBODs * pow(parameters.TSOD,(state.T_wat-20)) * state.C[rO2] / (state.C[rO2] + parameters.KSO2);
	state.BOD = state.C[rBOD1] + state.C[rBOD2] + state.C[rBOD1p] + state.C[rBOD2p];
	
	state.ConversionTermPerComponent[rO2] = (state.REAR + state.PO2M + state.SedO2 + state.BODox + state.Nitrif + state.SODT / state.d) * state.V;
	state.ConversionTermPerComponent[rBOD1] = (- parameters.Kd1 * state.C[rBOD1] * pow(parameters.TKd,(state.T_wat-20)) 
											   * state.C[rO2] / (state.C[rO2] + parameters.KO2) + parameters.SBOD1 / state.d) * state.V;
	state.ConversionTermPerComponent[rBOD2] = (- parameters.Kd2 * state.C[rBOD2] * pow(parameters.TKd,(state.T_wat-20)) 
											   * state.C[rO2] / (state.C[rO2] + parameters.KO2) + parameters.SBOD2 / state.d) * state.V;
  state.ConversionTermPerComponent[rBOD1p] = (- parameters.Vs1 / state.d - parameters.Kd1 * pow(parameters.TKd,(state.T_wat-20)) 
											    * state.C[rO2] / (state.C[rO2] + parameters.KO2)) * state.C[rBOD1p] * state.V;
	state.ConversionTermPerComponent[rBOD2p] = (- parameters.Vs2 / state.d - parameters.Kd2 * pow(parameters.TKd,(state.T_wat-20)) 
											    * state.C[rO2] / (state.C[rO2] + parameters.KO2)) * state.C[rBOD2p] * state.V;
	state.ConversionTermPerComponent[rNH4] = (- parameters.Knit * state.C[rNH4] * pow(parameters.TKnit,(state.T_wat-20)) 
										      * state.C[rO2] / (state.C[rO2] + parameters.KNO2) + parameters.SNH4 / state.d) * state.V;
	state.ConversionTermPerComponent[rBODs] = (parameters.Vs1 * state.C[rBOD1p] + parameters.Vs2 * state.C[rBOD2p] + state.SODT) / state.d * state.V;
	interface.DO = state.C[rO2];
  interface.NH4 = state.C[rNH4];
  interface.Q = state.Q_Out;
  state.DO_sat_perc = state.C[rO2] / state.CS;
  state.UIamm = state.C[rNH4] * 1 / (pow(10,(0.090182 + 2729.92 / (273.15 + state.T_wat) - parameters.pH)) +1);
  state.UIamm_corr = IF (state.C[rO2] >= 5)
										 THEN state.UIamm
										 ELSE state.UIamm * 2;
  state.DO_corr = IF (state.UIamm < 0.02)
									THEN state.C[rO2]
									ELSE IF (state.UIamm <= 0.15)
											 THEN state.C[rO2] - 0.97 * log(state.UIamm) - 3.8
											 ELSE state.C[rO2] - 2;
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
   };
   
 :};
