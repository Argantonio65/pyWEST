/*******************************************************************************
 *
 * Tornado - Advanced Kernel for Modeling and Virtual Experimentation
 * (c) Copyright 2004-2011 DHI
 *
 * This file is provided under the terms of a license and may not be
 * distributed and/or modified except where allowed by that license.
 *
 * This file is provided as is with no warranty of any kind, including the
 * warranty of design, merchantability and fitness for a particular purpose.
 *
 * $Revision: 1$
 * $Date: 29. juni 2012 15:52:27$
 *
 ******************************************************************************/

#ifndef WWTP_BASE_SBR
#define WWTP_BASE_SBR

CLASS SBRModel
EXTENDS SBRASMConversionModel WITH
  {:
  
  interface <-
  {
   OBJ Outflow2 (* terminal = "out_3" *) "bypass" : OutWWTPTerminal := {:causality <- "COUT" ; group <- "Effluent 2" :};
   OBJ Underflow (* terminal = "out_4" *) "effluent waste" : OutWWTPTerminal := {:causality <- "COUT" ; group <- "Underflow" :};
   OBJ T_Launch (* terminal = "in_2"; manip = "1" *) "Time constant to schedule the start up of one SBR in a multi-sbr system configuration " : Time 
     := {: causality <- "CIN" ; value <- 0.0; group <- "Operational" :};
   OBJ T1 (* terminal = "in_2"; manip = "1" *) "Period for the fill phase" : Time 
     := {: causality <- "CIN" ; value <- 0.125 ; group <- "Operational" :};
   OBJ T3 (* terminal = "in_2"; manip = "1" *) "Period for the settling phase" : Time 
     := {: causality <- "CIN" ; value <- 0.1; group <- "Operational" :};
   OBJ T4 (* terminal = "in_2"; manip = "1" *) "Period for the draw phase" : Time 
     := {: causality <- "CIN" ; value <- 0.025 ; group <- "Operational" :};
   OBJ T5 (* terminal = "in_2"; manip = "1" *) "Period for the idle phase" : Time 
     := {: causality <- "CIN" ; value <- 0.025 ; group <- "Operational" :};
   OBJ Q_Draw (* terminal = "in_2"; manip = "1" *) "Desired draw flow rate" : FlowRate 
       := {: causality <- "CIN" ; value <- 24000 ; group <- "Operational" :};
#if (defined ASM1Temp || defined ASM2dModTemp || defined ASM3Temp || defined ASM3P_EAWAG || defined ASM1_AN || defined ASMG1)
   OBJ Temp (* terminal = "in_2"; manip = "1" *)  "Temperature of the activated sludge"
  : CelsiusTemperature  := {: causality <- "CIN" ; value <- 15 ; group <- "Operational" :}; 
#endif
  };
  
  parameters <- 
  {
  };
  
  state <-
  {
  // CYCLI
    OBJ T "Length of total cycle" : Time := {: group <- "Operational" :};
    OBJ T2 "Period for the reaction phase" : Time := {: value <- 0.125 ; group <- "Operational" :};
    OBJ t_Begin (* hidden = "1" *) " Time at the beginning of the current cycle" : Time ;
  // INCOMING AND OUTGOING FLOW RATES
    OBJ Q_Out_Help (* hidden = "1" *) "Effluent flow rate help variable" : FlowRate ;
    OBJ Q_Bypass "Bypass flow rate" : FlowRate := {: group <- "Operational" :};
    };

  initial <- 
  {
   }; 
  
  equations <- 
  {
// CYCLI
   state.T = interface.T1 + state.T2 + interface.T3 + interface.T4 + interface.T5 ;

   state.t_Begin = IF (independent.t < interface.T_Launch)
             THEN interface.T_Launch
             ELSE interface.T_Launch + floor((independent.t-interface.T_Launch)/state.T) * state.T ;

#if (defined ASM1Temp || defined ASM2dModTemp || defined ASM3Temp || defined ASM3P_EAWAG || defined ASM1_AN || defined ASMG1)
    state.Temp_Actual = interface.Temp;
#endif
  };  
    :};

#ifdef StandardCategoryModels
CLASS SBRPointsettler
EXTENDS SBRModel WITH
 {:
  interface <-
   {
   OBJ AerationEnergy (* terminal = "out_2" *)"Aeration energy" : ElectricalEnergy := {: causality <- "COUT"; group <- "Energy" :};
   OBJ MixingEnergy (* terminal = "out_2" *)"Mixing energy" : ElectricalEnergy := {: causality <- "COUT"; group <- "Energy" :};
   OBJ PumpingEnergy (* terminal = "out_2" *)"Pumping energy" : ElectricalEnergy := {: causality <- "COUT"; group <- "Energy" :};
    };

  parameters <-
   {

// CYCLI
    
// FILLING AND REACTION
    
// OVERFLOW
    OBJ V_Max "Maximum volume of the tank" : Volume := {: value <- 2000 ; group <- "Dimension" :};
    OBJ V_Min "Minimum volume of the tank" : Volume := {: value <- 0 ; group <- "Dimension" :};
    OBJ N "Number of weirs on a tank" : PhysicalQuantityType :=
         {: value <- 100 ;
        group <- "Hydraulic" ;
            interval <- {:lowerBound <- 0; upperBound <- PLUS_INF; :}
         :} ;
    OBJ alfa "Parameter, function of the weir type or width" : PhysicalQuantityType := {: value <- 10000 ; group <- "Hydraulic" :};
    OBJ beta "Parameter, depends on the weir design" : PhysicalQuantityType := {: value <- 1 ; group <- "Hydraulic" :};
    OBJ A "Surface area of the tank" : Area := {: value <- 400 ; group <- "Dimension" :};
// SETTLING
    OBJ f_ns "Non-settleable fraction of suspended solids" :
         Fraction := {: value <- 0.005 ; group <- "Settling" :};
    OBJ F_Th "Thickening factor of the suspended solids" : Real := {: value <- 2 ; group <- "Settling":};
// DRAW
    
// IDLE
   
// TYPICAL MODEL PARAMETERS

// AERATION ENERGY (through KLa)
  OBJ OTR_Energy  "Oxygen transfer rate per energy input" : TransferRateEnergy := {: value <- 1800 ; group <- "Aeration" :}; 
    OBJ Period "Period over which the costs are calculated" : Time := {: value <- 1 ; group <- "Miscellaneous":};
// MIXING ENERGY
  OBJ ME_unit "Energy requirement per unit of volume for mixing in a AS tank" : Real := {: value <- 0.005; group <- "Mixing energy":};
  OBJ Kla_Min "Lowest kLa value that ensures adequate mixing" : OxygenTransferCoefficient :={: value <- 20.0; group <- "Mixing energy":};
    OBJ Mixing_When_Aerated "Mixing activity during aeration" : Real := {: value <- 0 ; group <- "Mixing energy":};
// PUMPING ENERGY
    OBJ F_Energy_FlowRate "Conversion factor Energy needed/Pump flow rate" : Ratio := {: value <- 0.04 ; group <- "Energy":};
    
   };

  state <-
   {

// DRAW
    OBJ Q_Out_Help_Draw (* hidden = "1" *) "Effluent flow rate help variable for the draw phase" : FlowRate ;
// IDLE
    OBJ Q_Under_Help_Waste (* hidden = "1" *) "Waste flow rate help variable for the idle phase" : FlowRate ;

// AERATION ENERGY (through KLa)
    OBJ Integ_AE (* hidden = "1" *)"Integral aeration energy" : ElectricalEnergy ;
// MIXING ENERGY
    OBJ Integ_ME (* hidden = "1" *)"Integral mixing energy" : ElectricalEnergy ;
  OBJ ME_Instant (* hidden = "1" *)"Instantaneous mixing energy" : Real:= {: group <- "Mixing":};
// PUMPING ENERGY
    OBJ Integ_PE (* hidden = "1" *)"Integral Pumping energy" : ElectricalEnergy ;
   };

  initial <-
   {
    };

  equations <-
   {
// INCOMING AND OUTGOING FLOW RATES
   
    state.Q_Out_Help = IF (state.V > parameters.V_Max)
                       THEN parameters.N * parameters.alfa
           * pow((state.V - parameters.V_Max)/parameters.A, parameters.beta)
                       ELSE 0;

    state.Q_Out_Help_Draw = IF (state.V > parameters.V_Min)
                        THEN interface.Q_Draw
                            ELSE 0;

    state.Q_Under_Help_Waste = IF (state.V > 0)
                             THEN parameters.Q_Waste
                             ELSE 0;

    state.Q_Out =
        IF (independent.t < interface.T_Launch)
      THEN 0
        ELSE
          IF (independent.t - state.t_Begin < (interface.T1))
      THEN state.Q_Out_Help
      ELSE
          IF (independent.t - state.t_Begin < (interface.T1 + state.T2))
          THEN 0
          ELSE
            IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3) )
            THEN 0
            ELSE
              IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3 + interface.T4))
              THEN state.Q_Out_Help_Draw
            ELSE 0;

   state.Q_Bypass =
       
       IF (independent.t < interface.T_Launch)
       THEN 0
     ELSE
         IF (independent.t - state.t_Begin < interface.T1)
     THEN 0
     ELSE
         IF (independent.t - state.t_Begin < (interface.T1 + state.T2))
         THEN state.Q_In
         ELSE
           IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3))
           THEN state.Q_In
         ELSE
             IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3 + interface.T4))
             THEN state.Q_In
             ELSE state.Q_In;

    state.Q_Under =
       IF (independent.t < interface.T_Launch)
       THEN 0
     ELSE
         IF (independent.t - state.t_Begin < interface.T1)
       THEN 0
     ELSE
         IF (independent.t - state.t_Begin < (interface.T1 + state.T2))
         THEN 0
         ELSE
           IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3))
           THEN 0
           ELSE
             IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3 + interface.T4))
             THEN 0
             ELSE state.Q_Under_Help_Waste;

// TOTAL MASS
    {FOREACH Comp_Index IN {IndexOfSolvent .. NrOfComponents}:
      
   DERIV(state.M[Comp_Index],[independent.t]) =
   IF (independent.t < interface.T_Launch)
   THEN state.ConversionTermPerComponent[Comp_Index]
   ELSE
     IF (independent.t - state.t_Begin < interface.T1)
     THEN state.FluxPerComponent[Comp_Index] + state.ConversionTermPerComponent[Comp_Index]
     ELSE
       IF (independent.t - state.t_Begin < (interface.T1 + state.T2))
       THEN state.ConversionTermPerComponent[Comp_Index]
       ELSE
         IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3))
         THEN 0
         ELSE
           IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3 + interface.T4))
           THEN state.FluxPerComponent[Comp_Index]
           ELSE state.FluxPerComponent[Comp_Index];
     };

// SETTLING

// CALCULATION OF OUTPUT INTERFACES

    interface.Outflow1[IndexOfSolvent] = - state.Q_Out / 0.000001;

    { FOREACH Comp_Index IN {IndexOfFirstSolubleComponent .. IndexOfLastSolubleComponent}:
       interface.Outflow1[Comp_Index] =  - state.C[Comp_Index] * state.Q_Out;
     };

    { FOREACH Comp_Index IN {IndexOfFirstParticulateComponent .. IndexOfLastParticulateComponent}:

       interface.Outflow1[Comp_Index] =
      IF (independent.t  < interface.T_Launch)
        THEN 0 
        ELSE
          IF (independent.t - state.t_Begin < interface.T1)
          THEN - state.C[Comp_Index] * state.Q_Out
          ELSE
            IF (independent.t - state.t_Begin < (interface.T1 + state.T2))
            THEN 0
            ELSE
              IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3))
              THEN 0
              ELSE
                IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3 + interface.T4))
                THEN - state.C[Comp_Index] * interface.Q_Draw * parameters.f_ns
                ELSE 0;
     };

    interface.Outflow2[IndexOfSolvent] = - state.Q_Bypass / 0.000001;

    { FOREACH Comp_Index IN {IndexOfFirstSolubleComponent .. NrOfComponents}:
      
      interface.Outflow2[Comp_Index] =
       IF (independent.t < interface.T_Launch)
       THEN 0
       ELSE
         IF (independent.t - state.t_Begin < interface.T1)
         THEN 0
         ELSE
           IF (independent.t - state.t_Begin < (interface.T1 + state.T2))
           THEN - state.InFluxPerComponent[Comp_Index]
           ELSE
             IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3))
             THEN - state.InFluxPerComponent[Comp_Index]
             ELSE
               IF (independent.t - state.t_Begin < (interface.T1 + state.T2 + interface.T3 + interface.T4))
               THEN - state.InFluxPerComponent[Comp_Index]
               ELSE - state.InFluxPerComponent[Comp_Index];
     };

    interface.Underflow[IndexOfSolvent] = - state.Q_Under / 0.000001;

    { FOREACH Comp_Index IN {IndexOfFirstSolubleComponent .. IndexOfLastSolubleComponent}:
       interface.Underflow[Comp_Index] =  - state.C[Comp_Index] *  state.Q_Under ;
     };

     { FOREACH Comp_Index IN {IndexOfFirstParticulateComponent .. IndexOfLastParticulateComponent}:
       interface.Underflow[Comp_Index] = - state.C[Comp_Index] * state.Q_Under * parameters.F_Th * (1-parameters.f_ns) ;
     };

// AERATION ENERGY
  DERIV(state.Integ_AE,[independent.t])= state.Kla_Actual * state.V ;
  interface.AerationEnergy = state.S_O_Saturation / (parameters.Period * parameters.OTR_Energy) * state.Integ_AE ;
// MIXING ENERGY
  state.ME_Instant = IF ((state.Kla_Actual < parameters.Kla_Min) || (state.Kla_Actual == 0))
            THEN parameters.ME_unit * state.V
          ELSE 
            IF (parameters.Mixing_When_Aerated)
            THEN parameters.ME_unit * state.V
            ELSE 0;   

  DERIV(state.Integ_ME,[independent.t])= state.ME_Instant;
  interface.MixingEnergy = state.Integ_ME ;
// PUMPING ENERGY
  DERIV(state.Integ_PE,[independent.t])=  state.Q_Under + state.Q_Out;
  interface.PumpingEnergy = parameters.F_Energy_FlowRate * state.Integ_PE ;
// Bypass is without pump
   };
 :};
#endif // StandardCategoryModels

#include "SBR/wwtp.base.SBR.sbrpointsettler1phasereact.msl"
#include "SBR/wwtp.base.SBR.sbrpointsettler2phasereact.msl"
#include "SBR/wwtp.base.SBR.sbrpointsettler3phasereact.msl"
#include "SBR/wwtp.base.SBR.sbrpointsettler4phasereact.msl"
#include "SBR/wwtp.base.SBR.sbrpointsettler5phasereact.msl"
#include "SBR/wwtp.base.SBR.sbrpointsettler6phasereact.msl"
#include "SBR/wwtp.base.SBR.sbrpointsettler7phasereact.msl"
#include "SBR/wwtp.base.SBR.sbrpointsettler8phasereact.msl"


#endif // WWTP_BASE_SBR
